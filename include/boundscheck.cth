include <iostream>

defmacro (array[index], array, index:
    if (array.name() == "lambda":
        # not actually an array access (general syntax ftl)
        return None
    )
    return quote(ceto.bounds_check(unquote(array), unquote(index)))
)

namespace (ceto)

if (defined(CETO_HAS_SOURCE_LOCATION):
    def (bounds_check, arr: mut:auto:ref:ref, index: size_t, loc: const:std.source_location:ref = std.source_location.current():
        if (index >= std.size(arr):
            std.cerr << "terminating on out of bounds access: " << loc.file_name() << ":"
            std.cerr << loc.function_name() << ":" << loc.line() << ":" << loc.column() << "\n"
            std.terminate()
        )

        return std.forward<decltype(arr)>(arr).unsafe_at(std.forward<decltype(index)>(index))
    ) : decltype(auto):requires:requires(std.size(arr))
else:
    def (bounds_check, arr: mut:auto:ref:ref, index: size_t:
        if (index >= std.size(arr):
            std.cerr << "terminating on out of bounds access\n"
            std.terminate()
        )

        return std.forward<decltype(arr)>(arr).unsafe_at(std.forward<decltype(index)>(index))
    ) : decltype(auto):requires:requires(std.size(arr))
) : preprocessor

def (bounds_check, non_array: mut:auto:ref:ref, obj: mut:auto:ref:ref:
    return std.forward<decltype(non_array)>(non_array).unsafe_at(std.forward<decltype(obj)>(obj))
) : decltype(auto):requires:not std.is_integral_v<std.remove_cvref_t<decltype(obj)>>

# from https://stackoverflow.com/questions/69785562/c-map-and-unordered-map-template-parameter-check-for-common-behavior-using-c/69869007#69869007
is_map_type: template<class:T>:concept = std.same_as<typename:T.value_type, std.pair<const:typename:T.key_type, typename:T.mapped_type>>

def (bounds_check, map_like: mut:auto:ref:ref, key: mut:auto:ref:ref:
    return std.forward<decltype(map_like)>(map_like).unsafe_at(std.forward<decltype(key)>(key))
) : decltype(auto):requires:std.is_integral_v<std.remove_cvref_t<decltype(key)>> and is_map_type<std.remove_cvref_t<decltype(map_like)>>
