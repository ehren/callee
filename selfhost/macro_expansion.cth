include <unordered_map>
include <ranges>
include <span>
include <functional>
include <ranges>

include (ast)
include (visitor)

if (_MSC_VER:
    include <windows.h>
    cpp'
    #define CETO_DLSYM GetProcAddress
    #define CETO_DLOPEN LoadLibraryA
    #define CETO_DLCLOSE FreeLibrary
    '
else:
    include <dlfcn.h>
    cpp'
    #define CETO_DLSYM dlsym
    #define CETO_DLOPEN(L) dlopen(L, RTLD_NOW)
    #define CETO_DLCLOSE dlclose
    '
) : preprocessor

struct (SemanticAnalysisError(std.runtime_error):
    pass
)

class (MacroDefinition:
    defmacro_node: Call
    pattern_node: Node
    body: Block
    parameters: std.map<string, Node>
    dll_path: std.string = {}
    impl_function_name: std.string = {}
)

class (MacroScope:
    _parent: weak:MacroScope:mut = {}
    macro_definitions: [MacroDefinition] = []

    def (parent:
        return self._parent.lock()
    ) : MacroScope:mut

    def (add_definition: mut, defn: MacroDefinition:
        self.macro_definitions.push_back(defn)
    )

    def (enter_scope: mut:
        m: mut = MacroScope()
        m._parent = self  # should really be using weak_from_this
        return m
    ) : MacroScope:mut
)

# def(name=functions_with_default_values, x: std.function = lambda(b), x: Identifier, b: ...:
#     return quote(unquote(x) : std.function(decltype(lambda(unpack(b)))) = lambda(unpack(b)))
# )

def (macro_matches, node: Node, pattern: Node, params: std.map<std.string, Node>:
    std.cout << "node: " << node.repr() << " pattern: " << pattern.repr()

    if (isinstance(pattern, Identifier):
        search = params.find(pattern.name().value())
        if (search != params.end():

            param_name = search->first
            matched_param = search->second
            if (isinstance(matched_param, Identifier):
                # wildcard match
                return std.map<std.string, Node>{{param_name, node}}
            elif (typeop = asinstance(matched_param, TypeOp)):
                ast_name = typeop.rhs()

                # constrained wildcard / match by type
                if (isinstance(ast_name, Identifier):
                    # handle base class wild cards specially:
                    if (ast_name.name() == "BinOp" and isinstance(node, BinOp):
                        return std.map<string, Node>{{param_name, node}}
                    elif ast_name.name() == "UnOp" and isinstance(node, UnOp):
                        return std.map<std.string, Node>{{param_name, node}}
                    )

                    node_class_name = node.classname()
                    if (node_class_name == typeop.rhs().name().value():
                        return std.map<std.string, Node>{{param_name, node}}
                    )
                )
            )
        )
    )

    if (typeid(*node) != typeid(*pattern):
        return {}
    )

    if (node.args.size() != pattern.args.size():
        return {}
    )

    if ((node.func == None) != (pattern.func == None):
        return {}
    )

    if (node.args.size() == 0 and node.func == None and pattern.func == None:
        if (node.repr() == pattern.repr():
            # leaf match
            return std.map<std.string, Node>{}
        )
        return {}
    )

    submatches: mut = std.map<std.string, Node> {}

    for (i in std.ranges.iota_view(0u, node.args.size()):
        m = macro_matches(node.args[i], pattern.args[i], params)
        if (not m:
            return {}
        )
        submatches.insert(m.begin(), m.end())  # std::optional autoderef
    )

    if (node.func:
        m = macro_matches(node.func, pattern.func, params)
        if (not m:
            return {}
        )
        submatches.insert(m.begin(), m.end())
    )

    return submatches
) : std.optional<std.map<std.string, Node>>


def (call_macro_impl, macro_impl_name: std.string, macro_dll_path: std.string, match: std.map<std.string, Node>:
    handle = CETO_DLOPEN(macro_dll_path.c_str())  # just leak it for now
    if (not handle:
        throw (std.runtime_error("Failed to open macro dll: " + macro_dll_path))
    )
    fptr = CETO_DLSYM(handle, macro_impl_name.c_str())
    if (not fptr:
        throw (std.runtime_error("Failed to find symbol " + macro_impl_name + " in dll " + macro_dll_path))
    )
    f = reinterpret_cast<decltype(+(lambda(m: std.map<string, Node>, None): Node))>(fptr)  # no explicit function ptr syntax yet/ever(?)
    return (*f)(match)
) : Node


struct (MacroDefinitionVisitor(BaseVisitor<MacroDefinitionVisitor>):
    on_visit_definition: std.function<void(MacroDefinition)>

    current_scope: MacroScope:mut = None
    replacements: std.unordered_map<Node, Node> = {}

    def (expand: mut, node: Node:
        scope: mut = self.current_scope
        while (scope:
            for (definition in std.views.reverse(scope.macro_definitions):
                match = macro_matches(node, definition.pattern_node, definition.parameters)
                if (match:
                    replacement = call_macro_impl(definition.impl_function_name, definition.dll_path, match.value())
                    if (replacement:
                        self.replacements[node] = replacement
                        return
                    )
                )
            )

            scope = scope.parent()
        )
    )

    def (visit: override:mut, node: Node.class:
        self.expand(ceto.shared_from(&node))

        if (node.func:
            node.func.accept(*this)
        )

        for (arg in node.args:
            arg.accept(*this)
        )
    )

    def (visit: override:mut, node: Call.class:
        self.expand(ceto.shared_from(&node))

        if (node.func.name() != "defmacro":
            node.func.accept(*this)

            for (arg in node.args:
                arg.accept(*this)
            )
            return
        )

        if (node.args.size() < 2:
            throw (SemanticAnalysisError("bad defmacro args"))
        )

        pattern = node.args[0]

        body = asinstance(node.args.back(), Block)
        if (not body:
            throw (SemanticAnalysisError("last defmacro arg must be a Block"))
        )

        parameters: mut = std.map<std.string, Node>{}

        match_args = std.span(node.args.cbegin() + 1, node.args.cend() - 1)
        for (arg in match_args:
            name = if (isinstance(arg, Identifier):
                arg.name().value()
            elif not isinstance(arg, TypeOp):
                throw (SemanticAnalysisError("bad defmacro param type"))
            elif not isinstance(arg.args[0], Identifier):
                throw (SemanticAnalysisError("bad typed defmacro param"))
            else:
                arg.args[0].name().value()
            )
            i = parameters.find(name)
            if (i != parameters.end():
                throw (SemanticAnalysisError("duplicate defmacro params"))
            )
            parameters.emplace(name, arg)
        )

        defn = MacroDefinition(ceto.shared_from(&node), pattern, body, parameters)
        self.current_scope.add_definition(defn)
        self.on_visit_definition(defn)
    )

    def (visit: override:mut, node: Module.class:
        s: mut = MacroScope()
        self.current_scope = s

        for (arg in node.args:
            arg.accept(*this)
        )
    )

    def (visit: override:mut, node: Block.class:
        outer = self.current_scope
        self.current_scope = outer.enter_scope()
        self.expand(ceto.shared_from(&node))
        for (arg in node.args:
            arg.accept(*this)
        )
        self.current_scope = outer
    )
)


def (expand_macros, node: Module, on_visit: std.function<void(MacroDefinition)>:
    visitor: mut = MacroDefinitionVisitor(on_visit)
    node.accept(visitor)
    return visitor.replacements
) : std.unordered_map<Node, Node>