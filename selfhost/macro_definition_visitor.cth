include <unordered_map>
include <ranges>
include <span>
include <functional>

include (ast)
include (visitor)


struct (SemanticAnalysisError(std.runtime_error):
    pass
)

struct (MacroDefinition:
    defmacro_node: Call
    pattern_node: Node
    body: Block
    parameters: std.map<string, Node>
    dll_path: std.string = {}
    impl_function_name: std.string = {}
)

class (MacroScope:
    _parent: weak:MacroScope = {}
    macro_definitions: [MacroDefinition] = []

    def (parent:
        return self._parent.lock()
    )

    def (add_definition: mut, defn: MacroDefinition:
        self.macro_definitions.push_back(defn)
    )

    def (enter_scope:
        m: mut = MacroScope()
        m._parent = self  # should really be using weak_from_this
        return m
    ) : MacroScope:mut
)

struct (MacroDefinitionVisitor(BaseVisitor<MacroDefinitionVisitor>):
    on_visit_definition: std.function<void(MacroDefinition)>

    macro_scopes: std.unordered_map<Node, MacroScope> = {}
    current_scope: MacroScope:mut = None

    def (visit: override:mut, node: Node.class:
        self.macro_scopes[ceto.shared_from(&node)] = self.current_scope

        if (node.func:
            node.func.accept(*this)
        )

        for (arg in node.args:
            arg.accept(*this)
        )
    )

    def (visit: override:mut, node: Call.class:
        if (node.func.name() != "defmacro":
            node.func.accept(*this)

            for (arg in node.args:
                arg.accept(*this)
            )
            return
        )

        if (node.args.size() < 2:
            throw (SemanticAnalysisError("bad defmacro args"))
        )

        pattern = node.args[0]

        body = asinstance(node.args.back(), Block)
        if (not body:
            throw (SemanticAnalysisError("last defmacro arg must be a Block"))
        )

        parameters: mut = std.map<std.string, Node>{}

        match_args = std.span(node.args.cbegin() + 1, node.args.cend() - 1)
        for (arg in match_args:
            name = if (isinstance(arg, Identifier):
                arg.name().value()
            elif not isinstance(arg, TypeOp):
                throw (SemanticAnalysisError("bad defmacro param type"))
            elif not isinstance(arg.args[0], Identifier):
                throw (SemanticAnalysisError("bad typed defmacro param"))
            else:
                arg.args[0].name().value()
            )
            i = parameters.find(name)
            if (i != parameters.end():
                throw (SemanticAnalysisError("duplicate defmacro params"))
            )
            parameters.emplace(name, arg)
        )

        defn = MacroDefinition(ceto.shared_from(&node), pattern, body, parameters)
        self.current_scope.add_definition(defn)
        self.on_visit_definition(defn)
    )

    def (visit: override:mut, node: Module.class:
        s: mut = MacroScope()
        self.macro_scopes[ceto.shared_from(&node)] = s
        self.current_scope = s

        for (arg in node.args:
            arg.accept(*this)
        )
    )

    def (visit: override:mut, node: Block.class:
        outer = self.current_scope
        inner = outer.enter_scope()
        self.macro_scopes[ceto.shared_from(&node)] = outer
        self.current_scope = inner
        for (arg in node.args:
            arg.accept(*this)
        )
        self.current_scope = outer
    )
)

def (visit_macro_definitions, node: Module, on_visit: std.function<void(MacroDefinition)>:
    visitor: mut = MacroDefinitionVisitor(on_visit)
    node.accept(visitor)
    return visitor.macro_scopes
)