include <pybind11/pybind11.h>

include (visitor)
include (ast)


struct (EvalableAstReprVisitor(BaseVisitor<EvalableAstReprVisitor>):
    # This should really just be a few virtual methods on Node, but we want to keep the visitor infrastructure in place for the future

    repr = ""

    def (visit: override:mut, node: Node.class:
        self.repr += class_name(&node) + "("

        if (node.func:
            node.func.accept(*this)
            self.repr += ", "
        )

        self.repr += "["
        if (node.args.size() > 0:
            for (arg in node.args:
                arg.accept(*this)
                self.repr += ", "
            )
        )
        self.repr += "], ("

        loc = std.get<1>(node.source)
        self.repr += '"", ' + std.to_string(loc) + "))"  # preserve source location but don't worry about the source string for now
    )

    def (visit: override:mut, node: UnOp.class:
        self.repr += class_name(&node) + '("' +  node.op + '", ['
        node.args[0].accept(*this)
        self.repr += "], ("
        loc = std.get<1>(node.source)
        self.repr += '"", ' + std.to_string(loc) + "))"
    )

    def (visit: override:mut, node: LeftAssociativeUnOp.class:
        self.repr += class_name(&node) + '("' +  node.op + '", ['
        node.args[0].accept(*this)
        self.repr += "], ("
        loc = std.get<1>(node.source)
        self.repr += '"", ' + std.to_string(loc) + "))"
    )

    def (visit: override:mut, node: BinOp.class:
        self.repr += class_name(&node) + '("' +  node.op + '", ['
        for (arg in node.args:
            arg.accept(*this)
            self.repr += ", "
        )
        self.repr += "], ("
        loc = std.get<1>(node.source)
        self.repr += '"", ' + std.to_string(loc) + "))"
    )

    def (visit: override:mut, node: Identifier.class:
        self.repr += class_name(&node) + '("' +  node.repr() + '"'
        self.repr += ", ("
        loc = std.get<1>(node.source)
        self.repr += '"", ' + std.to_string(loc) + "))"
    )

    def (visit: override:mut, node: StringLiteral.class:
        self.repr += class_name(&node) + "(" +  node.escaped() + ", "

        if (node.prefix:
            node.prefix.accept(*this)
        else:
            self.repr += "None"
        )
        self.repr += ", "

        if (node.suffix:
            node.suffix.accept(*this)
        else:
            self.repr += "None"
        )
        self.repr += ", ("
        loc = std.get<1>(node.source)
        self.repr += '"", ' + std.to_string(loc) + "))"
    )

    def (visit: override:mut, node: IntegerLiteral.class:
        self.repr += class_name(&node) + '("' + node.integer_string + '", '
        if (node.suffix:
            node.suffix.accept(*this)
        else:
            self.repr += "None"
        )
        self.repr += ", ("
        loc = std.get<1>(node.source)
        self.repr += '"", ' + std.to_string(loc) + "))"
    )
)
